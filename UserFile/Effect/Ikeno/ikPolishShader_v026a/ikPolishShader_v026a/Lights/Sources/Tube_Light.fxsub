//-----------------------------------------------------------------------------
//
//
//-----------------------------------------------------------------------------


//-----------------------------------------------------------------------------

#define CTRL_NAME	"(self)"

#if !defined(EnableLighTexture)
#define	EnableLighTexture	0
#define	TextureSize			256
#endif

#include "../ikPolishShader.fxsub"

#include "../Sources/constants.fxsub"
#include "../Sources/mmdparameter.fxsub"
#include "../Sources/mmdutil.fxsub"
#include "../Sources/colorutil.fxsub"

#include "./Sources/light.fxsub"
#include "./Sources/fog.fxsub"
#include "./Sources/omni_common.fxsub"

DECLARE_PARAM(float, mLightHeight, "ライト高+");

#define LightSizeMin	float2(0.1, LightHeightMin)
#define LightSizeV		float2(0, LightHeightMax)
#define LightSizeMax	(LightSizeMin + LightSizeV)
static float2 LightSize = float2(0.0, mLightHeight) * LightSizeV + LightSizeMin;

#define SHADOW_FX_FILE	"./Sources/omni_shadow.fx"
#define	SHADOW_RT_DESCRIPTION	"shadowmap for tube light"

#include "../Sources/structs.fxsub"
#include "../Sources/gbuffer.fxsub"
#include "../Sources/lighting.fxsub"
#include "../Sources/octahedron.fxsub"

#include "./Sources/omni_light.fxsub"

static const float3 LightRight = normalize(-LightObjectMatrix._11_12_13);
static const float3 LightUp = normalize(-LightObjectMatrix._21_22_23);
static const float3 LightDirection = normalize(LightObjectMatrix._31_32_33);


//-----------------------------------------------------------------------------
//

float3 GetClosestPosition(float3 wpos)
{
	float3 p0 = LightPosition - LightUp * LightSize.y;
	float3 ab = LightUp * LightSize.y * 2.0;
	float t = dot(wpos - p0, ab) / dot(ab, ab);
	return p0 + saturate(t) * ab;
}

float GetClosestSegment(float3 wpos, float3 r)
{
	float3 p0 = LightPosition - LightUp * LightSize.y;
	float3 p1 = LightPosition + LightUp * LightSize.y;
	float3 d = p0 - wpos;

	float dru = dot(r, LightUp);
	float drd = dot(r, d);
	float dud = dot(LightUp, d);
	float denom = max(1.0 - dru * dru, 1e-5);	// denom < 0.3 周辺になると精度が落ちる
	float d2 = (dru * drd - dud) / denom;

	// レイの後ろで交差する場合は、wposとの最近傍点を返す
	// float d1 = (drd - dru * dud) / denom;
	if (drd <= dru * dud)
	{
		d2 = -dot(d, LightUp);
	}

	return saturate(d2 / (LightSize.y * 2.0));
}

float3 GetClosestPosition(float3 wpos, float3 r)
{
	float t = GetClosestSegment( wpos, r);
	float3 closestPoint = LightPosition + LightUp * (LightSize.y * (t * 2.0 - 1.0));
/*
	// ライトの半径を考慮して太くする
	// from epic's siggraph 2013 notes
	// * Tube Lightsではなく、Sphere Lightsのところに説明がある
	float3 dir = closestPoint - wpos;
	float3 centerToRay = dot(dir, r) * r - dir;
	closestPoint = closestPoint + centerToRay * saturate(LightSize.x / length(centerToRay));
*/
	return closestPoint;
}

float CalcLineLightDiffuse(float3 wpos, float3 N)
{
#if 0
	// brute force
	#define DIFFUSE_STEP	8
	float dotNL = 0;
	float3 dv0 = LightUp * LightSize.y / DIFFUSE_STEP;
	float3 dp0 = (LightPosition - wpos) - dv0 * (DIFFUSE_STEP * 0.5);
	for(int i = 0; i < DIFFUSE_STEP; i++)
	{
		dotNL += saturate(dot(N, normalize(dp0 + dv0 * i)));
	}
	dotNL /= DIFFUSE_STEP;

#else
	float3 L0 = normalize(LightPosition - LightUp * LightSize.y - wpos);
	float3 L1 = normalize(LightPosition + LightUp * LightSize.y - wpos);
	float nume = 2.0 * saturate((dot(N,L0) + dot(N,L1)) * 0.5);
	float denom = dot(L0, L1) + 2.0;
	float dotNL = nume / denom;

#endif

	return dotNL;
}



//-----------------------------------------------------------------------------
//
VS_OUTPUT VS_SetTexCoord( float4 Pos : POSITION, float4 Tex : TEXCOORD0)
{
	VS_OUTPUT Out = (VS_OUTPUT)0; 

	Out.Pos = Pos;
	Out.TexCoord.xy = Tex.xy + ViewportOffset.xy;
	Out.TexCoord.w = LightRadius;
	return Out;
}


PS_OUT_MRT PS_DrawLight( float4 Tex: TEXCOORD0, uniform bool useShadow )
{
	float4 albedo = tex2D(ColorMap, Tex.xy);
	MaterialParam material = GetMaterial(Tex.xy, albedo);
	GeometryInfo geom = GetWND(Tex.xy);
	float3 wpos = geom.wpos;
	float depth = geom.nd.depth;
	float range = Tex.w;

	// 最近傍点
	float3 closestPos = GetClosestPosition(wpos);
	float shadow = CalcLightFalloff(wpos, closestPos, range);
	clip(shadow - 1e-4);

	float3 V = normalize(CameraPosition - wpos);
	float3 N = normalize(geom.nd.normal);
	float3 L0 = normalize(LightPosition - wpos);
	if (useShadow) shadow *= CalcShadow(float4(wpos,1), dot(N, L0), Tex.xy);

	//-----------------------------------------------------
	// デフューズ
	float dotNL = CalcLineLightDiffuse(wpos, N);
	// float dotNL = saturate(dot(N, normalize(closestPos - wpos)));
	float3 diffuse = LightColor * (dotNL * shadow);

	//-----------------------------------------------------
	// スペキュラ
	float3 R = normalize(reflect(-V, N));

#if 0
	// simple
	float3 p0 = GetClosestPosition(wpos, R);
	L = normalize(p0 - wpos);
	float3 specular = CalcSpecular(L, N, V, material.roughness, material.f0);
#elif 1
	// ad-hoc
	float t = GetClosestSegment(wpos, R);
	float3 p0 = LightPosition - LightUp * LightSize.y;
	float3 p1 = LightPosition + LightUp * LightSize.y;
	p0 = lerp(p0, closestPos, material.roughness);
	p1 = lerp(p1, closestPos, material.roughness);
	float3 p2 = lerp(p0, p1, t);
	float3 L00 = normalize(p0 - wpos);
	float3 L01 = normalize(p1 - wpos);
	float3 L02 = normalize(p2 - wpos);
	float3 specular0 = CalcSpecular(L00, N, V, material.roughness, material.f0);
	float3 specular1 = CalcSpecular(L01, N, V, material.roughness, material.f0);
	float3 specular2 = CalcSpecular(L02, N, V, material.roughness, material.f0);
	float3 specular = (lerp(specular1, specular0, t) + specular2) * 0.5;
#else
	// brute force
	// 線分が長くなると複数の点に見える
	float3 specular = 0;
{
	#define SPECULAR_STEP	8
	float3 sv0 = LightUp * LightSize.y / SPECULAR_STEP;
	float3 sp0 = (LightPosition - wpos) - sv0 * (SPECULAR_STEP * 0.5);
	for(int i = 0; i < SPECULAR_STEP; i++)
	{
		L = normalize(sp0 + sv0 * i);
		specular += CalcSpecular(L, N, V, material.roughness, material.f0);
	}
	specular *= (1.0 / SPECULAR_STEP);
}
#endif

	specular *= LightColor * shadow;

	PS_OUT_MRT Out;
	Out.Diffuse = float4(diffuse, 1);
	Out.Specular = float4(specular, 1);

	return Out;
}


//-----------------------------------------------------------------------------
//
#if FOG_TYPE == 2 && VOLUMETRIC_FOG > 0

VS_OUTPUT_FOG VS_DrawVolumeLight( float4 Pos : POSITION, float4 Tex : TEXCOORD0)
{
	VS_OUTPUT_FOG Out = (VS_OUTPUT_FOG)0; 

	Out.Pos = Pos;
	Out.TexCoord.xy = Tex.xy + ViewportOffset.xy;
//	Out.TexCoord.z = 0;
	Out.TexCoord.w = LightRadius;

	Out.PhaseCoef.xyz = CalcNormalizedMieCoef();
	Out.PhaseCoef.w = mFogIntensity;

	return Out;
}


float4 PS_DrawVolumeLight( float4 Tex: TEXCOORD0, float4 PhaseCoef: TEXCOORD2 ) : COLOR
{
	float3 fogTexCoord = CalcFogInterleavedTexCoord(Tex.xy);
	float2 uv = fogTexCoord.xy;
	float zorder = fogTexCoord.z;
	float3 V = GetViewDirection(uv);
	float maxDepth = MAX_FOG_DISTANCE;
	float startDepth = zorder * maxDepth;
	float slotLength = maxDepth / (FOG_SLICE_NUM + 1.0);
	float range = Tex.w;

	// 明らかに離れている場合は即座に終了する
	float3 lv = LightPosition - CameraPosition;
	float nearest = clamp(dot(V, lv), startDepth, startDepth + slotLength);
	float3 PosC = CameraPosition + V * nearest;
	float lightMax = CalcLightFalloff(PosC, LightPosition, range + LightSize.y);
	clip(lightMax - 1e-4);

	float3 PosB = CameraPosition + V * startDepth;
	float3 PosE = PosB + V * slotLength;

	float sum = 0;
	for(int i = 0; i < MAX_FOG_STEP; i++)
	{
		float t = i * 1.0 / MAX_FOG_STEP;
		float3 wpos = lerp(PosB, PosE, t);
		float3 L = normalize(LightPosition - wpos);
		float3 closestPos = GetClosestPosition(wpos);

		float light = CalcShadowSimple(float4(wpos,1));
		light *= CalcLightFalloff(wpos, closestPos, range);
		light *= GetFogPhase(V, L, PhaseCoef.xyz);
		sum += light;
	}

	sum.x *= PhaseCoef.w / MAX_FOG_STEP;
	float3 col = sum.x * LightColor;

	return float4(col, 0.0);
}
#endif

//-----------------------------------------------------------------------------
// 

#if EnableShadowMap > 0
#define OBJECT_TEC(name, mmdpass) \
	technique name < string MMDPass = mmdpass; \
		string Script =  \
			"RenderDepthStencilTarget=DepthBuffer;" \
			"RenderColorTarget0=ShadowMap; Pass=ConstructShadow;" \
			"RenderDepthStencilTarget=;" \
			FOG_SCRIPT \
			"RenderColorTarget1=PPPSpecularMapRT;" \
			"RenderColorTarget0=; Pass=DrawLight;" \
			"RenderColorTarget1=;"; \
	> { \
		pass ConstructShadow < string Script= "Draw=Buffer;"; > { \
			ZEnable = false;	ZWriteEnable = false; \
			VertexShader = compile vs_3_0 VS_ConstructShadow(); \
			PixelShader  = compile ps_3_0 PS_ConstructShadow(); \
		} \
		FOG_PASS \
		pass DrawLight < string Script= "Draw=Buffer;"; > { \
			ZEnable = false;	ZWriteEnable = false; \
			SrcBlend = ONE; DestBlend = ONE; \
			VertexShader = compile vs_3_0 VS_SetTexCoord(); \
			PixelShader  = compile ps_3_0 PS_DrawLight(true); \
		} \
	}
#else
#define OBJECT_TEC(name, mmdpass) \
	technique name < string MMDPass = mmdpass; \
		string Script =  \
			FOG_SCRIPT \
			"RenderColorTarget1=PPPSpecularMapRT;" \
			"RenderColorTarget0=; Pass=DrawLight;" \
			"RenderColorTarget1=;"; \
	> { \
		FOG_PASS \
		pass DrawLight < string Script= "Draw=Buffer;"; > { \
			ZEnable = false;	ZWriteEnable = false; \
			SrcBlend = ONE; DestBlend = ONE; \
			VertexShader = compile vs_3_0 VS_SetTexCoord(); \
			PixelShader  = compile ps_3_0 PS_DrawLight(false); \
		} \
	}
#endif


OBJECT_TEC(MainTec0, "object")
OBJECT_TEC(MainTecBS0, "object_ss")

//-----------------------------------------------------------------------------

