//-----------------------------------------------------------------------------
//

#include "../ikPolishShader.fxsub"

#include "../Sources/constants.fxsub"
#include "../Sources/structs.fxsub"
#include "../Sources/mmdparameter.fxsub"
#include "../Sources/mmdutil.fxsub"
#include "../Sources/colorutil.fxsub"
#include "../Sources/lighting.fxsub"

bool ExistPolish : CONTROLOBJECT < string name = "ikPolishShader.x"; >;


// 拡散反射
shared texture2D PPPDiffuseMap : RENDERCOLORTARGET;
sampler DiffuseMapSamp = sampler_state {
	texture = <PPPDiffuseMap>;
	MinFilter = LINEAR;	MagFilter = LINEAR;	MipFilter = NONE;
	AddressU  = CLAMP;	AddressV = CLAMP;
};

// 鏡面反射
shared texture2D PPPReflectionMap : RENDERCOLORTARGET;
sampler ReflectionMapSamp = sampler_state {
	texture = <PPPReflectionMap>;
	MinFilter = LINEAR;	MagFilter = LINEAR;	MipFilter = NONE;
	AddressU  = CLAMP;	AddressV = CLAMP;
};

// バックライト、ベルベット、クリアコートで法線を使う。それ以外は不要
#if !defined(DISABLE_NORMALMAP)
// 法線マップ
shared texture PPPNormalMapRT: RENDERCOLORTARGET;
sampler NormalMap = sampler_state {
	texture = <PPPNormalMapRT>;
	MinFilter = POINT;	MagFilter = POINT;	MipFilter = NONE;
	AddressU  = CLAMP;	AddressV = CLAMP;
};
#endif

#if USE_ALBEDO_MAP > 0
shared texture ColorMapRT: OFFSCREENRENDERTARGET;
sampler ColorMap = sampler_state {
	texture = <ColorMapRT>;
	MinFilter = POINT;	MagFilter = POINT;	MipFilter = NONE;
	AddressU  = CLAMP;	AddressV = CLAMP;
};
#endif


/*
#if IS_LIGHT > 0
float mLightIntensityP : CONTROLOBJECT < string name = "(self)"; string item = "ライト強度+"; >;
float mLightIntensityN : CONTROLOBJECT < string name = "(self)"; string item = "ライト強度-"; >;
static float3 LightIntensity = (mLightIntensityP * (MaxLightIntensity - 1) + 1.0) * saturate(1.0 - mLightIntensityN);
#endif
*/

//-----------------------------------------------------------------------------

float mDirectLightP : CONTROLOBJECT < string name = CONTROLLER_NAME; string item = "直接光+"; >;
float mDirectLightM : CONTROLOBJECT < string name = CONTROLLER_NAME; string item = "直接光-"; >;

bool bLinearBegin : CONTROLOBJECT < string name = "ikLinearBegin.x"; >;
bool bLinearEnd : CONTROLOBJECT < string name = "ikLinearEnd.x"; >;
static bool bOutputLinear = (bLinearEnd && !bLinearBegin);

// 座法変換行列
float4x4 matWVP			: WORLDVIEWPROJECTION;
float4x4 matWV			: WORLDVIEW;
float4x4 matW			: WORLD;
float4x4 matV			: VIEW;
float4x4 matLightWVP	: WORLDVIEWPROJECTION < string Object = "Light"; >;
float3	LightDirection	: DIRECTION < string Object = "Light"; >;
float3	CameraPosition	: POSITION  < string Object = "Camera"; >;

// ライト色
float3	LightDiffuse		: DIFFUSE   < string Object = "Light"; >;
float3	LightSpecular		: SPECULAR  < string Object = "Light"; >;

static float3	BaseAmbient = MaterialAmbient;
static float3	BaseEmissive = MaterialEmissive;

// ライトの強度
static float3 LightColor = LightSpecular * CalcLightValue(mDirectLightP, mDirectLightM, DefaultLightScale);

float ConvertToRoughness(float val) { return (1 - val) * (1 - val); }

#if !defined(ENABLE_CLEARCOAT)
#define	ENABLE_CLEARCOAT	0
#else
#if ENABLE_CLEARCOAT > 0
static float ClearcoatRoughness = ConvertToRoughness(ClearcoatSmoothness);

#include "../Sources/octahedron.fxsub"

shared texture PPPEnvMap2: RENDERCOLORTARGET;
sampler EnvMapSamp0 = sampler_state {
	texture = <PPPEnvMap2>;
	MinFilter = LINEAR;	MagFilter = LINEAR;	MipFilter = LINEAR;
	AddressU  = WRAP;	AddressV = WRAP;
};

texture2D EnvironmentBRDFTex <
	string ResourceName = "../Sources/Assets/EnvironmentBRDF.dds";
	// string Format = "A16B16G16R16F";
	int MipLevels = 1;
>;
sampler EnvironmentBRDF = sampler_state {
	texture = <EnvironmentBRDFTex>;
	MinFilter = LINEAR;	MagFilter = LINEAR;	MipFilter = NONE;
	AddressU  = CLAMP;	AddressV  = CLAMP;
};

static float MAX_MIP_LEVEL = log2(ENV_WIDTH) - 1.0;

float4 GetEnvColor(float3 vec, float roughness)
{
	float s = 1 - roughness;
	roughness = (1 - s * s);
	float lod = roughness * MAX_MIP_LEVEL;
	float2 uv = EncodeOctahedron(vec);
	return tex2Dlod(EnvMapSamp0, float4(uv,0,lod));
}

float3 ApplyClearCoat(float3 N, float3 NPoly, float3 L, float3 V, 
	float shadow, float3 bodyColor, inout float3 specular)
{
	float3 clearcoatN = normalize(lerp(N, NPoly, USE_POLYGON_NORMAL));
	float3 clearcoatR = reflect(-V, clearcoatN);
	float clearcoatNV = abs(dot(clearcoatN, V));

	float3 brdf = tex2D(EnvironmentBRDF, float2(ClearcoatRoughness, clearcoatNV)).xyz;
	float3 reflectance = (ClearcoatF0 * brdf.x + brdf.y);

	float3 diffuse = CalcDiffuse(L, clearcoatN, V) * shadow * LightColor;
	diffuse += GetEnvColor(clearcoatN, 1.0) * brdf.z;

	float coatThickness = (1 - clearcoatNV) * (1 - ClearcoatColor.a) + ClearcoatColor.a;
	coatThickness *= ClearcoatColor.a;
	bodyColor = lerp(bodyColor, ClearcoatColor.rgb * diffuse, coatThickness);
	specular *= lerp(1, ClearcoatColor.rgb, coatThickness);

	float3 ccSpecular;
	ccSpecular = CalcSpecular(L, clearcoatN, V, ClearcoatRoughness, ClearcoatF0);
	ccSpecular *= LightColor * shadow;
	ccSpecular += GetEnvColor(clearcoatR, ClearcoatRoughness) * reflectance;
	specular = lerp(specular, ccSpecular, ClearcoatIntensity);

	return bodyColor;
}
#endif
#endif

/*
#if !defined(DISABLE_NORMALMAP)
float4 AdjustTexCoord(float4 nd, inout float2 texCoord)
{
	float4 nd0 = tex2D(NormalMap, texCoord);
	float4 nd1 = tex2D(NormalMap, texCoord + float2(-1, 0) / ViewportSize);
	float4 nd2 = tex2D(NormalMap, texCoord + float2( 1, 0) / ViewportSize);
	float4 nd3 = tex2D(NormalMap, texCoord + float2( 0,-1) / ViewportSize);
	float4 nd4 = tex2D(NormalMap, texCoord + float2( 0, 1) / ViewportSize);

	float d0 = abs(nd0.w - nd.w);
	float d1 = abs(nd1.w - nd.w);
	float d2 = abs(nd2.w - nd.w);
	float d3 = abs(nd3.w - nd.w);
	float d4 = abs(nd4.w - nd.w);

	// エッジではない
	if (d0 < 1.0)
	{
		return nd0;
	}

	if (d1 < 1.0) texCoord.x -= 1.0 / ViewportSize.x;
	if (d2 < 1.0) texCoord.x += 1.0 / ViewportSize.x;
	if (d3 < 1.0) texCoord.y -= 1.0 / ViewportSize.y;
	if (d4 < 1.0) texCoord.y += 1.0 / ViewportSize.y;

	return nd;
}
#endif
*/


#if ENABLE_HAIR_SPECULAR > 0
//-----------------------------------------------------------------------------
// 髪の毛のスペキュラ

#if !defined(HAIR_CENTER_BONE_NAME)
#define HAIR_CENTER_BONE_NAME	"頭"
#endif
float4x4 mHeadMat : CONTROLOBJECT < string name = "(self)"; string item = HAIR_CENTER_BONE_NAME; >;
float3 mHeadPos : CONTROLOBJECT < string name = "(self)"; string item = HAIR_CENTER_BONE_NAME; >;

float3 ComputeHairTangent(float3 N, float3 V, float3 WPos, float2 UV)
{
	// Tは根本から毛先方向を指す。
#if 0
	// タンジェントマップを見て決める
	float3 dp1 = ddx(V);
	float3 dp2 = ddy(V);
	float2 duv1 = ddx(UV);
	float2 duv2 = ddy(UV);
	float3x3 M = float3x3(dp1, dp2, cross(dp1, dp2));
	float2x3 inverseM = float2x3(cross(M[1], M[2]), cross(M[2], M[0]));
	float3 T = normalize(mul(float2(duv1.x, duv2.x), inverseM));
	float3 B = normalize(mul(float2(duv1.y, duv2.y), inverseM));
	float2 t = tex2D(TangentMap, UV).xy;
	T = normalize(T * t.x + B * t.y);
#else
	// 頭ボーンの下方向を向く
	// 距離が離れるほど頭の中心からの距離にする
	float3 T0 = -mHeadMat[1].xyz;
	float3 T1 = WPos - mHeadPos;
	float l = length(T1);
	T1 /= max(l, 1);
	T0 = normalize(lerp(T0, T1, saturate(l - 5.0) ));
		// 50cm〜60cmに掛けて接線の向きを補間する

	float3 B = normalize(cross(N, T0));
	float3 T = normalize(cross(B, N));
#endif

	return T;
}

// Gaussian distribution
float HairGaussian(float beta, float theta)
{
	#define SQRT_2PI	2.50662827		// sqrt(2.0 * PI) ≒ 2.5
	float beta2 = 2.0 * beta * beta;
	float theta2 = theta * theta;
//	return exp(-theta2 / beta2) / sqrt(PI * beta2);
	return exp(-theta2 / beta2) / (beta * SQRT_2PI);
}

// Marschnerを適当に改造したもの
float3 SimpleHairSepc(float3 N, float3 T, float3 V, float3 L, float smoothness, float3 attenuation)
{
	float shift = 3.0 * DEG_TO_RAD;	// キューティクルの傾き
	float roughness = lerp(10.0, 5.0, smoothness) * DEG_TO_RAD;	// 表面の粗さ。
	float t = 0.75; // 透過率

	float alphaR = -1.0 * shift;
	float alphaTT = 0.5 * shift;
	float alphaTRT = 2.0 * shift;
	float betaR = 1.0 * roughness;
	float betaTT = 0.5 * roughness;
	float betaTRT = 2.0 * roughness;

	float TL = dot(T, L);
	float thetaI = asin(TL);
	float thetaR = asin(dot(T, V));
	float thetaH = (thetaR + thetaI) * 0.5;
//	float thetaD = (thetaR - thetaI) * 0.5;

	float M_R = HairGaussian(betaR, thetaH - alphaR);
	float M_TT = HairGaussian(betaTT, thetaH - alphaTT);
	float M_TRT = HairGaussian(betaTRT, thetaH - alphaTRT);

	// 適当な色の減衰：経路が長いほど色が減衰する。
	float l = 1.0 / (abs(TL) + 0.1);
	float3 N_TT = exp(-l * attenuation);
	float3 N_TRT = N_TT * N_TT;

	// 適当な反射/透過率：光の総和を1以下に抑えるための処理
	float cosPhi = dot(N,L);
	float T_R = (1.0 - t) * saturate(cosPhi);
	float T_TT = (t * t) * saturate(cosPhi * -0.5 + 0.5);
	float T_TRT = (t * (1.0 - t) * t) * 1.0;

	return (M_R * T_R + M_TT *T_TT * N_TT + M_TRT * T_TRT * N_TRT) * HairSpecularIntensity;
}

float3 CalcHairColor()
{
	// 減衰色のブースト
	float3 attenuation = saturate(1.0 - Degamma(MaterialToon));
	float g0 = Luminance(attenuation);
	attenuation *= attenuation;
	attenuation *= attenuation;
	float g1 = Luminance(attenuation);
	attenuation = attenuation * g0 / max(g1, 0.01) + 0.01;
	return attenuation;
}

float3 GetHairSpecular(float3 N, float3 V, float3 L, float3 WPos, float2 uv, float3 attenuation)
{
	float3 T = ComputeHairTangent(N, V, WPos, uv);
	float3 hairSpec = SimpleHairSepc(N, T, V, L, HairSmoothness, attenuation);
	hairSpec *= Luminance(LightSpecular);
	return hairSpec;
}
#endif


//-----------------------------------------------------------------------------
//

float3 CalcNormalizedToon()
{
	float3 result = 1;
	if (use_toon)
	{
		float3 linearColor = Degamma(MaterialToon);
		float g = Luminance(linearColor);
		result = lerp(1, linearColor / max(g, 0.01), saturate(ToonColor_Scale));
	}

	return result;
}

float3 CalcToonLight(float3 c, float3 toonColor)
{
	float g = saturate(Luminance(c) * 2.0 - 0.5);
	return c * lerp(toonColor, 1, g);
}

static float3 MaterialBaseColor = Degamma(
	((!use_toon) ? MaterialDiffuse.rgb : BaseAmbient)
	#if IS_LIGHT > 0
		+ MaterialEmissive
	#endif
);

//-----------------------------------------------------------------------------
// セルフシャドウ用Z値プロット

struct VS_ZValuePlot_OUTPUT {
	float4 Pos : POSITION;				// 射影変換座標
	float4 ShadowMapTex : TEXCOORD0;	// Zバッファテクスチャ
};

VS_ZValuePlot_OUTPUT ZValuePlot_VS( float4 Pos : POSITION, float2 Tex : TEXCOORD0 )
{
	VS_ZValuePlot_OUTPUT Out = (VS_ZValuePlot_OUTPUT)0;
	Out.Pos = mul( Pos, matLightWVP );
	Out.ShadowMapTex = Out.Pos;
	return Out;
}

float4 ZValuePlot_PS( float4 ShadowMapTex : TEXCOORD0, float2 Tex : TEXCOORD1 ) : COLOR
{
	return float4(ShadowMapTex.z/ShadowMapTex.w,0,0,1);
}

technique ZplotTec < string MMDPass = "zplot"; > {
	pass ZValuePlot {
		AlphaBlendEnable = FALSE;
		VertexShader = compile vs_3_0 ZValuePlot_VS();
		PixelShader  = compile ps_3_0 ZValuePlot_PS();
	}
}


//-----------------------------------------------------------------------------
// オブジェクト描画

struct BufferShadow_OUTPUT {
	float4 Pos		: POSITION;		// 射影変換座標

	float4 Tex		: TEXCOORD0;	// テクスチャ
	float4 Normal	: TEXCOORD1;	// 法線, 深度
	float3 Eye		: TEXCOORD2;	// カメラとの相対位置
	float4 PPos		: TEXCOORD3;	// スクリーン座標
	#if IGNORE_SPHERE == 0
	float2 SpTex	: TEXCOORD4;	// スフィアマップテクスチャ座標
	#endif
	float4 ToonColor	: TEXCOORD5;

	#if ENABLE_HAIR_SPECULAR > 0
	float4 WPos		: TEXCOORD6;
	float4 HairColor	: TEXCOORD7;
	#endif
};

BufferShadow_OUTPUT DrawObject_VS(
	VS_AL_INPUT IN, uniform bool useSelfShadow)
{
	BufferShadow_OUTPUT Out = (BufferShadow_OUTPUT)0;

	float4 Pos = IN.Pos;
	float3 Normal = IN.Normal.xyz;

	Out.Pos = mul( Pos, matWVP );

	float4 WPos = mul( Pos, matW );
	Out.Eye = CameraPosition - WPos.xyz;

	Out.Normal.xyz = normalize( mul( Normal, (float3x3)matW ) );
	Out.Normal.w = mul(Pos, matWV).z;

	Out.PPos = Out.Pos;
	Out.Tex.xy = IN.Tex;

	#if IGNORE_SPHERE == 0
	if ( use_spheremap && use_subtexture) Out.SpTex = IN.AddUV1.xy;
	#endif

	Out.ToonColor.rgb = CalcNormalizedToon();

	#if ENABLE_HAIR_SPECULAR > 0
	Out.WPos = WPos;
	Out.HairColor.rgb = CalcHairColor();
	#endif

	return Out;
}



float4 DrawObject_PS(BufferShadow_OUTPUT IN, uniform bool useSelfShadow) : COLOR
{
	float3 L = -LightDirection;
	float3 V = normalize(IN.Eye);
	float3 N = normalize(IN.Normal.xyz);
	float3 NPoly = N;

	float2 texCoord = IN.PPos.xy / IN.PPos.w * float2(0.5, -0.5) + 0.5;
	texCoord += ViewportOffset;

	// 素材自体の色
	float4 albedo = float4(MaterialBaseColor,1);

	#if !defined(DISABLE_NORMALMAP)
	// float4 nd = AdjustTexCoord(IN.Normal, texCoord);
	float4 nd = tex2D(NormalMap, texCoord);
	N = normalize(nd.xyz);
	#endif

	if ( use_texture )
	{
		#if USE_ALBEDO_MAP > 0
		albedo.rgb *= tex2D(ColorMap, texCoord).rgb;
		#else
		albedo *= Degamma(GetTextureColor(IN.Tex.xy));
		#endif
	}

	float3 subSpecular = 0;
	#if IGNORE_SPHERE == 0
	if ( use_spheremap ) {
		float2 SpTex = mul( N, (float3x3)matV ).xy * float2(0.5, -0.5) + 0.5;
		float4 TexColor = GetSphereColor(use_subtexture ? IN.SpTex : SpTex);
		if(spadd) {
			subSpecular = TexColor.rgb * LightSpecular * SphereScale;
		} else {
			albedo.rgb *= (Degamma(TexColor.rgb) * SphereScale + (1.0 - SphereScale));
		}
	}
	#endif

	#if defined(ENABLE_VELVET) && ENABLE_VELVET > 0
	float velvetLevel = pow(1.0 - abs(dot(N,V)), VelvetExponent);
	velvetLevel = saturate(velvetLevel * (1.0 - VelvetBaseReflection) + VelvetBaseReflection);
	float3 velvetMulCol = lerp(VELVET_MUL_COLOR, VELVET_MUL_RIM_COLOR, velvetLevel);
	float3 velvetAddCol = lerp(VELVET_ADD_COLOR, VELVET_ADD_RIM_COLOR, velvetLevel);
	albedo.rgb = saturate(albedo.rgb * velvetMulCol + velvetAddCol);
	#endif

	// ライトの計算
	float4 diffusemap = tex2D(DiffuseMapSamp, texCoord);
	float4 specmap = tex2D(ReflectionMapSamp, texCoord);
	float shadow = (useSelfShadow) ? diffusemap.w : 1;

	// 拡散反射(直接光+環境光)
	float3 light = diffusemap.rgb;

	// 鏡面反射
	float3 specular = specmap.rgb + subSpecular;
	// 髪の毛のスペキュラ
	#if ENABLE_HAIR_SPECULAR > 0
	float3 hairSpec = GetHairSpecular(N, V, L, IN.WPos.xyz, texCoord, IN.HairColor.rgb);
	// return float4(hairSpec * saturate(diffusemap.rgb), 1);
	specular += hairSpec * saturate(diffusemap.rgb);
	#endif

	// 最終的な色の計算
	light = CalcToonLight(light, IN.ToonColor.rgb);
	if (!ExistPolish) light = 1; // 適当
	float4 result = float4(light, MaterialDiffuse.a) * albedo;

	#if Enable_Cutout > 0
	clip(result.a - CutoutThreshold);
	result.a = 1;
	#endif

	// クリアコート層
	#if ENABLE_CLEARCOAT > 0
	result.rgb = ApplyClearCoat(N, NPoly, L, V, shadow, result.rgb, specular);
	#endif

	#if ENABLE_SPECULAR_ALPHA > 0
	// スペキュラに応じて不透明度を上げる。
	result.rgb = result.rgb * result.a + specular;
	float2 luminnance = max(result.rg, result.ba);
	float alpha = saturate(max(luminnance.x, luminnance.y));
	result.rgb /= max(alpha, 1.0/1024);
	result.a = alpha;
	#else
	result.rgb += specular;
	#endif

	result.rgb = bOutputLinear ? result.rgb : Gamma(result.rgb);

	return result;
}


#define OBJECT_TEC(name, mmdpass, selfshadow) \
	technique name < string MMDPass = mmdpass; bool UseSelfShadow = selfshadow;\
	> { \
		pass DrawObject { \
			VertexShader = compile vs_3_0 DrawObject_VS(selfshadow); \
			PixelShader  = compile ps_3_0 DrawObject_PS(selfshadow); \
		} \
	}


OBJECT_TEC(MainTec0, "object", false)
OBJECT_TEC(MainTecBS0, "object_ss", true)

technique EdgeTec < string MMDPass = "edge"; > {}
technique ShadowTec < string MMDPass = "shadow"; > {}

//-----------------------------------------------------------------------------

