

////////////////////////////////////////////////////////////////////////////////////////////////
// RSM

#if defined(RSMCount) && RSMCount > 0

// ライト座標系での奥行きを出力
texture RSMMapRT : OFFSCREENRENDERTARGET <
	string Description = "ReflectiveShadowMap for ikPolish";
	int Width = RSM_TEX_SIZE;
	int Height = RSM_TEX_SIZE;
	string Format = "A16B16G16R16F";	// 法線+深度
	float4 ClearColor = { 0, 0, 0, 0 };
	float ClearDepth = 1.0;
	int MipLevels = 1;
	string DefaultEffect = 
		HIDE_EFFECT
		"*.pmd = RSM/RSMLightBuffer.fx;"
		"*.pmx = RSM/RSMLightBuffer.fx;"
		"*.x =   RSM/RSMLightBuffer.fx;"
		"* = hide;" ;
>;

sampler RSMLightSamp = sampler_state {
	texture = <RSMMapRT>;
	MinFilter = POINT; MagFilter = POINT;	MipFilter = NONE;
	AddressU = BORDER; AddressV = BORDER; BorderColor = float4(0,0,0,0);
};

shared texture PPPRSMAlbedoMapRT : RENDERCOLORTARGET <
	int Width = RSM_TEX_SIZE;
	int Height = RSM_TEX_SIZE;
	string Format = "A8R8G8B8" ;
	int MipLevels = 1;
>;
sampler RSMAlbedoSamp = sampler_state {
	texture = <PPPRSMAlbedoMapRT>;
	MinFilter = POINT; MagFilter = POINT;	MipFilter = NONE;
	AddressU = BORDER; AddressV = BORDER; BorderColor = float4(0,0,0,0);
};


//-----------------------------------------------------------------------------
//

float3 TargetPosition : CONTROLOBJECT < string name = "(self)"; >;
#include	"./RSM/rsm_common.fxsub"


// シャドウマップの深度からWPosの再構築＆比較
inline GeometryInfo ReconstructWPosFromLight(float2 texCoord)
{
	NormalDepth nd = (NormalDepth)tex2Dlod(RSMLightSamp, float4(texCoord, 0,0));
	float depth = nd.depth;

//	float2 uv = (texCoord * 2 - 1.0) * float2(1,-1) * 0.5;
	float2 uv = texCoord * float2(1,-1) - (float2(1,-1) * 0.5);

	GeometryInfo tmp;
	tmp.nd = nd;
	tmp.wpos = mul(float4(uv * RSM_ShadowSize, depth, 1), lightInvMatV).xyz;
	return tmp;
}



//-----------------------------------------------------------------------------
//
float4 PS_CalcRSM( float4 Tex: TEXCOORD0) : COLOR
{
	float2 texCoord = Tex.zw;
	int2 iuv = floor(texCoord * WorkSize);
	GeometryInfo geom0 = GetWND(texCoord);

	float4 lightPPos = mul(float4(geom0.wpos, 1), lightMatVP);
	texCoord = lightPPos.xy / lightPPos.w * float2(0.5, -0.5) + 0.5;

	float angleMul = 7.0 / RSMCount * (PI * 2.0);
	float angleAdd = GetJitterOffset(iuv) * (PI * 2.0);
	float radiusMul = RSMRadius * 1.0 / (RSM_ShadowSize * RSMCount);
	float radiusAdd = 1.0 / RSM_TEX_SIZE;
	float scale = RSMRadius * RSMRadius * 2.0 / (RSMCount * RSMCount);

	float4 bounce = 0;

	// [unroll]
	for(int j = 0; j < RSMCount; j++)
	{
		float2 sc;
		sincos(j * angleMul + angleAdd, sc.x, sc.y);
		float2 uv = sc * (j * radiusMul + radiusAdd) + texCoord;

		GeometryInfo geom1 = ReconstructWPosFromLight(uv);

		float3 v = geom1.wpos - geom0.wpos;
		float invl2 = 1.0 / max(dot(v, v), 1.0);
		float w0 = max( dot(geom0.nd.normal, v) * invl2, 0);
		float w1 = max(-dot(geom1.nd.normal, v) * invl2, 0);
			// dot(?,v) * dot(?,v) * invl2 * invl2 で 1.0 / length(v)^2 相当
		float w2 = (j + 0.5);	// sample weight
			// 近くほど探索密度が高いので、結果に占める割合を減らす。
			// 実際は w2 = (j + 0.5) * 2.0 / RSMCount だが、定数はscale に追い出してある。

		float4 col = tex2Dlod(RSMAlbedoSamp, float4(uv, 0,0));
		bounce += float4(col.rgb, 1) * (w0 * w1 * w2);
	}

	bounce = saturate(bounce * scale);
	bounce.rgb *= LightColor;

	return bounce;
}

inline float4 GetRSMColor(float2 Tex)
{
	#if WORKSPACE_RES != 1
	return tex2D(HalfWorkSamp2, Tex);
	#else
	return tex2D(FullWorkSamp, Tex);
	#endif
}

#else

inline float4 GetRSMColor(float2 Tex) { return 0; }

#endif


